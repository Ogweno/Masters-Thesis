* SAC macro to to make a slowness-section with picked arrival times.  
* Input (files keyword) is assumed to be in the form for the
* "sacslantstack" program:
*    file-name alignment-time amplitude(not used)
*
* Each file has the name of a main phase arrival in its KA header variable,
* which corresponds to its alignment time.
*
* Keywords:
*   port -  portrait plot format; otherwise landscape
*   files x - phase alignment times in file x
*   pat x - ls pattern to match receiver function files
*   tables - travel time tables (for the ttimes program; default iasp91) 
*   xlim -  time window relative to alignment phase
*   range - for record section, slowness range (in sec/degree; default 0-15);
*           for picture, low high and increment moveout relative to P.
*   unit -  s/deg or s/km; s/deg is default
*   phase - phases to be drawn on the record section.  First phase is the
*           alignment phase.  These can also be discontinuity interaction
*           phases (P410s, P660s, etc.).  Also can plot specific multiples
*           related to an interface, using the syntax XXX/H,Vp,VpVs where
*           XXX is a comma-separated list of one or more of Ps, PpPs, PsPs or
*           PpSs and H is depth, Vp is P velocity in layer and VpVs is the
*           Vp/Vs ratio in layer.
*   hdr x - header variable name x has slowness in s/deg or s/km (see unit)
*   shift - explicit shift of alignment phase (default: 0)
*   title - title string to put onto plot
*   polarity - [honor|ignore] honor or ignore polarity of picks (default: honor)
*   embedded - "yes" if this is part of a larger plot (omit BEGINFRAME/ENDFRAME)
*   sum x - write zero slowness stack sum to file x if given
*   size x - boost amplitude of each trace by factor x (default 1.0)
*   bin x - average traces in bins of width x
*   label x - label each trace with header variable x
*   ylabel x - Y label for traces
*   picture [[nthroot n] | [phaseweight n]] - make picture, using nthroot or
*           phase weight stacking, stack exponent n.  Default nthroot 1
*           (linear stack).  Range parameter expresses relative moveout from P
*           for picture stacks, note.
*   contour - only contour, no gray scale picture
*   magt [x y]... - magnify between x and y (more than one x y pair possible)
*   magp [x y]... - magnify between x and y (more than one x y pair possible)
* 
*   G. Helffrich/U. Bristol
*      Last updated 24 Feb. 2012
$keys tables range xlim files shift phase title embedded polarity sum size bin pat label ylabel picture dir magt magp debug hdr contour unit port
$default xlim -5 45
$default tables default
$default range 0 15
$default shift 0 
$default phase P
$default title none
$default embedded no
$default polarity honor
$default sum no
$default size 1.0
$default bin none
$default pat none
$default files none
$default label off
$default ylabel default
$default picture no
$default magt no
$default magp no
$default debug no
$default hdr phase
$default contour no
$default unit s/deg
$default port no
$default dir ~geo-g4/seismo/sacmacro
if $tables NE default
   setbb tables $tables$
endif
if _ EQ "_%tables%"
   setbb tttab default
else
   setbb tttab %tables%
endif
if $bin NE none
   setbb ss_macro_bin "bin $bin$ ./tmp_pbin"
else
   setbb ss_macro_bin ""
endif
if _no ne "_$port$"
   setbb ss_orient port
   setbb ss_vlab "Ray parameter @($unit$@)"
   setbb ss_hlab "$ylabel$"
else
   setbb ss_orient land
   setbb ss_hlab "Ray parameter @($unit$@)"
   setbb ss_vlab "$ylabel$"
endif
if _no NE _$picture
   setbb ss_macro_mag ""
   setbb ss_macro_bin "picture (item 3 $range$)"
   if (itemcount $picture$) gt 0
      setbb ss_macro_bin append " (items 1 2 $picture$) ./tmp_pbin"
   else
      setbb ss_macro_bin append " nthroot 1 ./tmp_pbin"
   endif
   if _no NE _$magt
      sc to bb echo $magt | awk '{for@(i=1;i<NF;i+=2@) print @$@(i@),@$@(i+1@)}'
      while read bb lo hi
        setbb ss_macro_mag append " -magt max $lo$ $hi$"
      enddo
   endif
   if _no NE _$magp
      sc to bb echo $magp | awk '{for@(i=1;i<NF;i+=2@) print @$@(i@),@$@(i+1@)}'
      while read bb lo hi
        setbb ss_macro_mag append " -magp max $lo$ $hi$"
      enddo
   endif
else
   setbb ss_macro_bin 'section'
   setbb ss_macro_mag "_"
endif
if "$hdr$" ne phase
   setbb ss_macro_bin append " hdr $hdr$ $unit$"
else
   setbb ss_macro_bin append " phase ka $unit$"
endif
if none ne $pat$
* run/endrun used here instead of sc to prevent truncation of long lines
$run sh > ./tmp_rs.pcf
ls $pat$ |
awk '{print \@$1,0.0,1.0}' |
sh $dir$/ss-stack.sh %tttab% (items 1 2 $range$) $xlim '$phase$' $shift$ ./tmp_ptime '%ss_macro_bin%' '%ss_macro_mag%' %ss_orient% 
$endrun
else
sc sh $dir$/ss-stack.sh %tttab% (items 1 2 $range$) $xlim '$phase$' $shift$ ./tmp_ptime '%ss_macro_bin%' '%ss_macro_mag%' %ss_orient% < $files > ./tmp_rs.pcf
endif
setbb ss_macro_bin (change honor off (change ignore on $polarity$))
if none ne "$title$"
   setbb rs_macro_title $title$
   $run cat - >> ./tmp_rs.pcf
[f3ssvbhc]
t 0.75 0.08
%rs_macro_title%
$endrun
endif
sc echo "Q" >> ./tmp_rs.pcf
* sc cat ./tmp_rs.pcf
if _no EQ _$picture
   if (status nfiles) gt 0
      dc all
   endif
   sss
   zerostack
   timewindow $xlim
   distancewindow fixed $range units km 
   macro ./tmp_ptime
   border off
   if $embedded eq no
      beginframe
   endif
   prs referenceline off labels $label$ weight off polarity %ss_macro_bin% size $size$ xlabel "(quotebb ss_hlab)" ylabel "(quotebb ss_vlab)" orient %ss_orient%
   plotc replay file ./tmp_rs
   if $embedded eq no
      endframe
   endif
   border on
   if $sum$ ne no
      sumstack ; writestack $sum$
   endif
   quitsub
else
   r ./tmp_pbin
   div &1,depmax&
   if $embedded eq no
      beginframe
   endif
   xlabel "Time @(s@)"
   if _$unit eq '_s/deg'
      ylabel "Moveout @(deg@)"
   else
      ylabel "Moveout @(km@)"
   endif
   zlevels list 0.75 0.90 0.95 0.99
   if _no eq _$contour$
      grayscale color
   endif
   contour; plotc replay file ./tmp_rs
*  xlabel previous ; ylabel previous
   zlevels scale; xlabel off; ylabel off
   if $embedded eq no
      endframe
   endif
endif
if _no ne _$debug
   sc cat ./tmp_rs.pcf
endif
sc /bin/rm -f ./tmp_ptime ./tmp_rs.pcf ./tmp_pbin
unsetbb rs_macro_depth rs_macro_title
unsetbb ss_hlab ss_vlab ss_macro_bin ss_macro_mag ss_orient
