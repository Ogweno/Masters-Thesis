* eqdecon macro takes as parameters a file name prefix and component suffix set.
* It builds input and runs the deconvolution measurement program.  The output 
* files generated contain deconvolved radial, tangential and vertical comps.
* Assumes A variable in file header set to begin of decon window, F 
* variable set to end.  If variables not set, then you are prompted.
* Other parameters:
*    file xxxx - prefix to file names
*    comps ce cn cz - three component suffixes, east north vertical components.
*       File names are thus xxxx.ce xxxx.cn xxxx.cz, OK?
*    phase xxx - name of phase to deconvolve, just a comment.
*    pick [ yes | maybe ] - If yes, force re-picking of seismogram
*    fill x - water level fill value (typically 0.01 <= x <= 0.0001)
*    fp a - low-pass filter parameter to eliminate high frequency noise
*       from result (due to fourier transform method to do convolution).
*       Choose a from this table:
*         a   Frequency (hz) at which G(f) = 0.1 
*         10      4.8
*          5.211  3.0
*          5      2.4  (this is a typical choice)
*          4.141  2.0
*          2.5    1.2
*          2.070  1.0
*          1.25   0.6
*          1.0352 0.5
*          0.625  0.3
*          0.5    0.24
*          0.4    0.2
*          0.2    0.1
*    shift x - shift time in receiver function by x seconds.  This allows
*       pre-arrival waveform behavior to be monitored to assess deconvolution
*       parameter quality.
*    tables x - use travel time tables x for resetting file origin; use "NONE"
*       for your tables if you don't want this feature.
*
****   NOTE before opening any windows type the following command to get
*     proper windowing
*   window 1 x 0.05 0.80 y 0.05 0.74
* then type 'bd xwindow' or 'bd sunwindow'
****
*
$keys file comps phase pick fill fp shift tables
$default phase default
$default pick maybe
$default fp 2.5
$default shift 20.0
$default tables ak135
setbb pick $pick
setbb files " "
do sfx list $comps
   setbb files "%files% $file$.$sfx$"
enddo
setbb sta (CONCAT (BEFORE "." "$file$") ".dpar")
message "%files%"
rh %files%
* Phase being measured is taken from KA marker in the file, if present.
if $phase eq default
   if UNDEFINED ne &1,ka
      setbb phase &1,ka
   else
      setbb phase P
   endif
else
   setbb phase $phase
endif
* do loop below isn't really a loop, but is used so that the BREAK command
*   will exit if no markers set during PPK.
do loopo list once
if &1,a eq UNDEFINED
   setbb pick yes
endif
if %pick% eq yes
   r %files% ; rmean ; m ttsac phase %phase
   m prompt "Hit return, then pick arrival time - A begins window, F ends it"
   if "%prompt%_" ne "_"
      break
   endif
*  r %files% ; plotpk zero markall on
   plotpk zero markall on
   if &1,a eq UNDEFINED
      break
   endif
   setbb beg &1,a& ; setbb end &1,f&
   rh %files% ; ch a %beg f %end ka %phase kf _ ; wh %files%
endif
* Build input file for deconvolution program, then invoke it.
sc cat /dev/null > ./decon.in
sc echo "&1,a& &1,f& %phase%" >> ./decon.in
sc echo "$fill$ $fp$ $shift$" >> ./decon.in
* message "/home/george/seismo/src/Rftn.Codes/RForward/Decon/deconeqn %files% < ./decon.in"
sc /home/george/seismo/src/Rftn.Codes/RForward/Decon/deconeqn %files% < ./decon.in
sc touch %sta%
sc echo "$file$ $fill$ $fp$" >> %sta%
if $tables$ ne NONE
   sc cat /dev/null > ./decon.in
   sc echo "$file$.eqr %phase% 0.0" >> ./decon.in
   sc echo "$file$.eqt %phase% 0.0" >> ./decon.in
   sc echo "$file$.eqz %phase% 0.0" >> ./decon.in
   sc sacsetomarker -model $tables$ < ./decon.in
endif
r $file$.eqr $file$.eqt $file$.eqz ; p1
enddo
unsetbb files phase pick
sc /bin/rm -f ./decon.in
